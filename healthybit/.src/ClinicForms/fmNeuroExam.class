' Gambas class file

Private $encid As String
Private $sType As String

Public Sub _new(encid As String, sType As String)

  $encid = encid
  $sType = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me, "False")
  Me.Icon = Picture[modGeneralMain.$strLogo]
  Me.Title = "ENC: " & $encid
  PictureBox1.Picture = Image.Load("Images/neuro.png").Stretch(PictureBox1.Width, PictureBox1.Height).Picture
  GetDefaultValue()

End

Public Sub btncredits_Click()

  Dim xx As String

  xx = "[Credits]<br><br><b> Dr Lekhjung Thapa</b><br> DM (Neurology)"
  Message.Info(xx, ("OK"))

End

Private Sub GetDefaultValue()
  ''GCS

  txtgcsE.Value = 4
  txtgcsV.Value = 5
  txtgcsM.Value = 6

  ''Reflex
  txtbicepR.Value = 2
  txtbicepL.Value = 2

  txtbicepR.Value = 2
  txttricepL.Value = 2

  txtsupiR.Value = 2
  txtsupiL.Value = 2

  txtkneeR.Value = 2
  txtkneeL.Value = 2

  txtankleR.Value = 2
  txtankleL.Value = 2

  ''Motor Power
  txtmototupProxR.Value = 5
  txtmototupProxL.Value = 5

  txtmototupDistR.Value = 5
  txtmototupDistL.Value = 5

  txtmotorLowProxR.Value = 5
  txtmotorLowProxL.Value = 5

  txtmotorLowDIstR.Value = 5
  txtmotorLowDistL.Value = 5

  txtmotorLowEHLR.Value = 5
  txtmotorLowEHLL.Value = 5

  txtmotorLowFHLR.Value = 5
  txtmotorLowFHLL.Value = 5

  ''MMSE
  txtmmseval.Value = 30

End

Public Sub btnsave_Click()

  Dim xlist As String[] = ["MMSE", "Nystagmus", "Neck Rigidity", "Tandem Gait"]
  Dim xgcslst As String[] = ["Eye Response", "Verbal Response", "Motor Response"]
  Dim aColl As JSONCollection
  Dim gcsval As Integer

  Dim xcont As Control
  Dim xspinbar As SpinBox
  Dim xradbutton As RadioButton
  Dim xslider As SliderBox
  Dim xtlbtn As ToolButton
  Dim xleft As String
  Dim xright As String
  Dim xtext As String
  Dim xval As String

  Dim xboolean As Boolean

  If modBasic.$ClinHistoryInput = "Single" Then
    xboolean = True
  Else
    xboolean = False
  Endif

  ''General Complaints
  If TextArea1.Text Then
    modPatientGeneral.AddExamGeneralQualiData($encid, "History", "General Complaints", "", TextArea1.Text, xboolean)
  Endif

  ''GCS
  gcsval = 0
  aColl = New JSONCollection
  For Each xval In xgcslst
    For Each xcont In Me.Controls
      If xcont Is SpinBox Then
        xspinbar = xcont
        If xspinbar.Tag = xval Then
          gcsval = gcsval + xspinbar.Value
          aColl.Add(xspinbar.Value, xval)
        Endif
      Endif
    Next
  Next
  If gcsval Then
    modClinSub.AddClinicExam($encid, "", "Glasgow Coma Scale (GCS)", "", JSON.Encode(aColl), gcsval, False, $sType, "Glasgow_Coma_Scale", "Regular")
    xtext = ""
  Endif

  ''Other Examinations (without Left/Right Values)
  For Each xval In xlist
    For Each xcont In Me.Controls

      If xcont Is SpinBox Then
        xspinbar = xcont
        If xspinbar.Tag = xval Then
          If xspinbar.Value Then
            modClinSub.AddQuantiData($encid, "", xspinbar.Tag, "", xspinbar.Value, False, $sType, "")
          Endif
        Endif
      Else If xcont Is SliderBox Then
        xslider = xcont
        If xslider.Tag = xval Then
          If xslider.Value Then
            modClinSub.AddQuantiData($encid, "", xslider.Tag, "", xslider.Value, False, $sType, "")
          Endif
        Endif
      Else If xcont Is RadioButton Then
        xradbutton = xcont
        If xradbutton.Tag = xval Then
          If xradbutton.Value = True Then
            If xradbutton.Text = "Norm" Then
              xtext = "Normal"
            Else If xradbutton.Text = "Abnorm" Then
              xtext = "Abnormal"
            Else
              xtext = xradbutton.Text
            Endif
            modClinSub.AddClinicExam($encid, "", xradbutton.Tag, "", xtext, 0, False, $sType, "", "Regular")
            xtext = ""
          Endif
        Endif

      Else If xcont Is ToolButton Then
        xtlbtn = xcont
        If xtlbtn.Tag Then
          modClinSub.AddClinicExam($encid, "", xtlbtn.Tooltip, "", xtlbtn.Tag, 0, False, $sType, "", "Regular")
        Endif
      Endif

    Next
  Next

  ''Left/Right Examinations
  For Each xval In modMedicine.GetNeuroDualVarList()
    For Each xcont In Me.Controls
      If xcont Is SpinBox Then
        xspinbar = xcont
        If xspinbar.Value Then
          If xspinbar.Tag = xval & " (Left)" Then
            xleft = CStr(xspinbar.Value)
          Else If xspinbar.Tag = xval & " (Right)" Then
            xright = CStr(xspinbar.Value)
          Endif
        Endif

      Else If xcont Is SliderBox Then
        xslider = xcont
        If xslider.Value Then
          If xslider.Tag = xval & " (Left)" Then
            xleft = CStr(xslider.Value)
          Else If xslider.Tag = xval & " (Right)" Then
            xright = CStr(xslider.Value)
          Endif
        Endif

      Else If xcont Is RadioButton Then
        xradbutton = xcont
        If xradbutton.Value = True Then
          If xradbutton.Text = "Norm" Then
            xtext = "Normal"
          Else If xradbutton.Text = "Abnorm" Then
            xtext = "Abnormal"
          Else
            xtext = xradbutton.Text
          Endif
          If xradbutton.Tag = xval & " (Left)" Then
            xleft = xtext
          Else If xradbutton.Tag = xval & " (Right)" Then
            xright = xtext
          Endif
        Endif

      Endif
    Next

    If xleft Or If xright Then
      modClinSub.AddClinicExam($encid, "", xval, "", modString.GetLefRightJSON(xleft, xright), 0, False, $sType, "", "Regular")
    Endif
    xleft = ""
    xright = ""
  Next

  Balloon.Info(("Information saved"), btnsave)
  Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnreport_Click()

  Dim $BillingReport As CReportHTML
  Dim asx As New String[0]
  Dim xval As String
  Dim xleft As String
  Dim xright As String
  Dim xtext As String

  Dim xcont As Control
  Dim xspinbar As SpinBox
  Dim xradbutton As RadioButton
  Dim xslider As SliderBox
  Dim i As Integer

  Inc Application.Busy
  $BillingReport = New CReportHTML([("SNo"), ("Variable"), ("Value")], "PatientReport", $encid)
  $BillingReport.UserData.Add("NEUROLOGY EXAMINATION", "Report")
  $BillingReport.UserData.Add(modReportVar.GetDateTimeReport(Now(), gb.GeneralDate), "PARAM1")

  i = 1
  For Each xval In modMedicine.GetNeuroDualVarList()

    For Each xcont In Me.Controls
      If xcont Is SpinBox Then
        xspinbar = xcont
        If xspinbar.Value Then
          If xspinbar.Tag = xval & " (Left)" Then
            xleft = CStr(xspinbar.Value)
          Else If xspinbar.Tag = xval & " (Right)" Then
            xright = CStr(xspinbar.Value)
          Endif
        Endif

      Else If xcont Is SliderBox Then
        xslider = xcont
        If xslider.Value Then
          If xslider.Tag = xval & " (Left)" Then
            xleft = CStr(xslider.Value)
          Else If xslider.Tag = xval & " (Right)" Then
            xright = CStr(xslider.Value)
          Endif
        Endif

      Else If xcont Is RadioButton Then
        xradbutton = xcont
        If xradbutton.Value = True Then
          If xradbutton.Text = "Norm" Then
            xtext = "Normal"
          Else If xradbutton.Text = "Abnorm" Then
            xtext = "Abnormal"
          Endif
          If xradbutton.Tag = xval & " (Left)" Then
            xleft = xtext
          Else If xradbutton.Tag = xval & " (Right)" Then
            xright = xtext
          Endif
        Endif

      Endif
    Next

    If xleft Or If xright Then
      With asx
        .Add(CStr(i))
        .Add(xval)
        .Add(modString.GetDualTableFormat([xleft], [xright], ["LEFT", "RIGHT"]))
      End With
      $BillingReport.Add(asx)
      asx.Clear()
      i = i + 1
    Endif
    xleft = ""
    xright = ""

  Next
  Dec Application.Busy
  modControlSub.OpenHTMLPreview($encid, $BillingReport.NewHTMLPath(), "ReportSize")

End

Public Sub btncervical_Click()

  Dim xList As String[] = ["Normal", "Tenderness", "Deformity"]
  Dim xx As String

  xx = InputCombo("Select Observation", "Cervical Spine", xList, "", True)
  If xx Then
    btncervical.Tag = xx
  Endif

End

Public Sub btnthoracic_Click()

  Dim xList As String[] = ["Normal", "Tenderness", "Deformity"]
  Dim xx As String

  xx = InputCombo("Select Observation", "Thoracic Spine", xList, "", True)
  If xx Then
    btnthoracic.Tag = xx
  Endif

End

Public Sub btnlumbar_Click()

  Dim xList As String[] = ["Normal", "Tenderness", "Deformity"]
  Dim xx As String

  xx = InputCombo("Select Observation", "Lumbar Spine", xList, "", True)
  If xx Then
    btnlumbar.Tag = xx
  Endif

End

Public Sub btnsacro_Click()

  Dim xList As String[] = ["Normal", "Tenderness", "Deformity"]
  Dim xx As String

  xx = InputCombo("Select Observation", "Sacrococcygeal Spine", xList, "", True)
  If xx Then
    btnsacro.Tag = xx
  Endif

End

Public Sub btndraw_Click()

  Dim sPath As String
  Dim xxx As String[]
  Dim xval As String

  xxx = CustomDraw()
  If xxx Then
    sPath = xxx[0]
    If sPath Then
      xval = modImage.SaveClinicScreenDraw($encid, xxx[1], sPath)
    Endif
  Endif

End

Public Sub btnvitals_Click()

  Dim hForm As FmVital

  hForm = New FmVital($encid)
  hForm.ShowModal

End

Public Sub btnsymptom_Click()

  Dim sName As String[]

  sName = GridListView(("Select Symptoms"), modMedicine.GetSymptomAllTypeList(), TextArea1)
  If sName And If sName.Count Then
    TextArea1.Text = sName.Join(gb.NewLine)
  Endif

End

Public Sub btnicd_Click()

  Dim sName As String[]

  If $encid Then
    sName = ICDTree(modDatabase.$icdConn, "Disease", modBasic.$ClinDiagnoSelectGrouped, modBasic.$ClinDiagnoSelectERGroup, modBasic.$ClinDiagnoChapterGrouped, modBasic.$DefaultDiagnoGroup)
    If sName Then
      If sName[1] And If sName[0] Then
        modPatientGeneral.AddPatientFindings("Provisional Diagnosis", $encid, sName[0], sName[1], sName[2])
      Endif
    Endif
  Endif

End
